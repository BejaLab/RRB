
rule rhod_cluster_bestrhodopsins_and_related:
    input:
        "input/global_phylogeny/rhodopsins/rhodopsin_bestrhodopsins_and_related.fasta"
    output:
        "analysis/global_phylogeny/rhodopsins/rhodopsin_bestrhodopsins_and_related.cdhit"
    params:
        c = 0.5,
        n = 2
    conda:
        "envs/tools.yaml"
    shell:
        "cdhit -i {input} -o {output} -d 0 -c {params.c} -n {params.n}"

rule rhod_cat:
    input:
        "analysis/global_phylogeny/rhodopsins/rhodopsin_bestrhodopsins_and_related.cdhit",
        "input/global_phylogeny/rhodopsins/rhodopsin_outgroups.fasta",
        "input/global_phylogeny/rhodopsins/rhodopsin_selected_8TMs_from_uniref50.fasta"
    output:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.fasta"
    conda:
        "envs/tools.yaml"
    shell:
        "seqkit seq {input} -o {output}"

rule rhod_cdhit:
    input:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.fasta"
    output:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.cdhit"
    params:
        c = 0.9,
        n = 2
    conda:
        "envs/tools.yaml"
    shell:
        "cdhit -i {input} -o {output} -c {params.c} -n {params.n} -d 0"

rule rhod_mafft:
    input:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.cdhit"
    output:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.mafft"
    threads:
        40
    params:
        mode = [ "--maxiterate 1000", "--localpair" ]
    conda:
        "envs/mafft_global.yaml"
    shell:
        "mafft --thread {threads} --reorder {params.mode} {input} > {output}"

rule rhod_trimal1:
    input:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.mafft"
    output:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.trimal"
    params:
        gt = 0.8
    conda:
        "envs/tools.yaml"
    shell:
        "trimal -in {input} -out {output} -gt {params.gt}"

rule rhod_getlong:
    input:
        trimal = "analysis/global_phylogeny/rhodopsins/rhodopsins.trimal",
        mafft = "analysis/global_phylogeny/rhodopsins/rhodopsins.mafft"
    output:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.mafft.long"
    params:
        min_len = 180
    conda:
        "envs/tools.yaml"
    shell:
        "seqkit seq -ni -gm{params.min_len} {input.trimal} | seqkit grep -f- {input.mafft} -o {output}"

rule rhod_trimal2:
    input:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.mafft.long"
    output:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.trimal.long"
    params:
        gt = 0.9
    conda:
        "envs/tools.yaml"
    shell:
        "trimal -in {input} -out {output} -gt {params.gt}"

rule rhod_trim_to_domain:
    input:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.trimal.long"
    output:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.trimal.domain.fasta"
    params:
        offset = 10,
        residue = "K"
    conda:
        "envs/tools.yaml"
    shell:
        """
        max=$(hmmbuild /dev/stdout {input} | awk -v offset={params.offset} -v residue={params.residue} '$23==residue {{print $22+offset}}' | tail -n1)
        seqkit subseq -r "1:$max" {input} | seqkit rmdup -so {output}
        """

rhod_seeds = [ 9277, 11160, 12613, 13639, 14953, 16110, 17291, 18533, 19633, 20998, 22076, 23467, 24639, 25765, 27041, 28300, 29390, 30649, 31889, 33058 ]

rule rhod_iqtree:
    input:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.trimal.domain.fasta"
    output:
        "analysis/global_phylogeny/rhodopsins/iqtree/{seed}/rhodopsins.treefile",
        "analysis/global_phylogeny/rhodopsins/iqtree/{seed}/rhodopsins.log"
    params:
        prefix = "analysis/global_phylogeny/rhodopsins/iqtree/{seed}/rhodopsins",
        bb = 1000,
        pers = 0.2,
        nstop = 500,
    threads:
        2
    conda:
        "envs/tools.yaml"
    shell:
        "iqtree2 -s {input} -pre {params.prefix} -seed {wildcards.seed} -bb {params.bb} -T {threads} -redo -pers {params.pers} -nstop {params.nstop}"

rule rhod_select_iqtree:
    input:
        log_files  = expand("analysis/global_phylogeny/rhodopsins/iqtree/{seed}/rhodopsins.log", seed = rhod_seeds),
        tree_files = expand("analysis/global_phylogeny/rhodopsins/iqtree/{seed}/rhodopsins.treefile", seed = rhod_seeds)
    output:
        log_file  = "output/global_phylogeny/rhodopsins_iqtree.log",
        tree_file = "output/global_phylogeny/rhodopsins_iqtree.treefile"
    conda:
        "envs/r.yaml"
    script:
        "scripts/select_global_tree.R"

rule rhod_interproscan:
    input:
        "analysis/global_phylogeny/rhodopsins/rhodopsins.fasta"
    output:
        "analysis/global_phylogeny/rhodopsins/interproscan/rhodopsins.fasta.tsv"
    params:
        dir = "analysis/global_phylogeny/rhodopsins/interproscan"
    threads:
        20
    shell:
        "interproscan.sh -i {input} -d {params.dir} -cpu {threads}"

rule rhod_get_taxonomy:
    input:
        "input/global_phylogeny/rhodopsins/rhodopsin_selected_8TMs_from_uniref50.fasta"
    output:
        "analysis/global_phylogeny/rhodopsins/uniref50.taxonomy"
    conda:
        "envs/r.yaml"
    script:
        "scripts/get_taxonomy.R"

rule rhod_plot_tree:
    input:
        tree     = "output/global_phylogeny/rhodopsins_iqtree.treefile",
        selected_8TMs = "input/global_phylogeny/rhodopsins/rhodopsin_selected_8TMs_from_uniref50.fasta",
        fasta    = "analysis/global_phylogeny/rhodopsins/rhodopsins.fasta",
        taxa     = "input/global_phylogeny/taxa.txt",
        interpro = "analysis/global_phylogeny/rhodopsins/interproscan/rhodopsins.fasta.tsv",
        domains  = "input/global_phylogeny/rhodopsins/rhodopsin_curated_interpro_domains.txt",
        taxonomy = "analysis/global_phylogeny/rhodopsins/uniref50.taxonomy"
    output:
        "output/global_phylogeny/rhodopsins_iqtree.pdf"
    conda:
        "envs/r.yaml"
    script:
        "scripts/plot_global_rhodopsins.R"
