
best_seeds = [ 675042, 675045, 675051, 675823, 676882, 678437, 679257, 680587, 681566, 682770, 683843, 685109, 686159, 687414, 688501, 689720, 690775, 692247, 693136, 694567 ]

rule best_download_alignment:
    output:
        "input/global_phylogeny/bestrophins/bestrophins/uniprot_aln.fasta"
    params:
        pfam = "PF01062"
    shell:
        "curl 'https://pfam.xfam.org/family/{params.pfam}/alignment/uniprot/format?format=fasta&alnType=uniprot&order=t&case=l&gaps=dashes&download=1' > {output}"

rule best_download_uniref50_fasta:
    output:
        "input/global_phylogeny/bestrophins/bestrophins/uniref50.fasta"
    params:
        pfam = "PF01062",
        id = 0.5
    shell:
        "curl 'https://www.uniprot.org/uniref/?query=uniprot:(database:(type:pfam+{params.pfam}))+identity:{params.id}&format=fasta&force=true' > {output}"

rule best_download_uniref50_tab:
    output:
        "input/global_phylogeny/bestrophins/bestrophins/uniref50.txt"
    params:
        pfam = "PF01062",
        id = 0.5,
        columns = "id,reviewed,name,count,members,organisms,length"
    shell:
        "curl 'https://www.uniprot.org/uniref/?query=uniprot:(database:(type:pfam+{params.pfam}))+identity:{params.id}&format=tab&force=true&columns={params.columns}' > {output}"

rule best_select_uniref50_trim:
    input:
        fas = "input/global_phylogeny/bestrophins/bestrophins/uniprot_aln.fasta",
        ur50 = "input/global_phylogeny/bestrophins/bestrophins/uniref50.txt"
    output:
        "analysis/global_phylogeny/bestrophins/ur50_aln.faa"
    conda:
        "envs/tools.yaml"
    shell:
        "cut -f1 {input.ur50} | tail -n+2 | cut -f2 -d_ | seqkit grep -rf- {input.fas} | seqkit replace -sp [a-z] -r - -o {output}"

rule best_uniref50_faidx:
    input:
        "analysis/global_phylogeny/bestrophins/ur50_aln.faa"
    output:
        "analysis/global_phylogeny/bestrophins/ur50_aln.faa.fai"
    conda:
        "envs/tools.yaml"
    shell:
        "seqkit faidx {input}"

rule best_uniref50_min_len:
    input:
        fas = "analysis/global_phylogeny/bestrophins/ur50_aln.faa",
        fai = "analysis/global_phylogeny/bestrophins/ur50_aln.faa.fai"
    output:
        "analysis/global_phylogeny/bestrophins/ur50_aln_long.faa"
    params:
        min_len = 240
    conda:
        "envs/tools.yaml"
    shell:
        "seqkit seq -nigm{params.min_len} {input.fas} | xargs seqkit faidx {input.fas} > {output}"

rule best_bouncer:
    input:
        "analysis/global_phylogeny/bestrophins/ur50_aln_long.faa"
    output:
        "analysis/global_phylogeny/bestrophins/ur50_aln_long_output_clean.fasta"
    params:
        g = 10,
        k = 0.25
    shell:
        "SequenceBouncer.py -i {input} -g {params.g} -k {params.k}"

rule best_bouncer_plain:
    input:
        "analysis/global_phylogeny/bestrophins/ur50_aln_long_output_clean.fasta"
    output:
        "analysis/global_phylogeny/bestrophins/ur50_long.fasta"
    shell:
        "seqkit seq -g -o {output} {input}"

rule best_cdhit:
    input:
        "analysis/global_phylogeny/bestrophins/ur50_long.fasta"
    output:
        cdhit = "analysis/global_phylogeny/bestrophins/ur50_long.cdhit",
        clstr = "analysis/global_phylogeny/bestrophins/ur50_long.cdhit.clstr"
    params:
        n = 2,
        c = 0.5
    threads:
        40
    shell:
        "cdhit -i {input} -o {output} -n {params.n} -c {params.c} -T {threads}"

rule best_cdhit_filter:
    input:
        cdhit = "analysis/global_phylogeny/bestrophins/ur50_long.cdhit",
        fas = "analysis/global_phylogeny/bestrophins/ur50_aln.faa",
        fai = "analysis/global_phylogeny/bestrophins/ur50_aln.faa.fai"
    output:
        "analysis/global_phylogeny/bestrophins/ur50_aln_filtered.faa"
    shell:
        "seqkit seq -ni {input.cdhit} | xargs seqkit faidx {input.fas} > {output}"

rule best_trimal:
    input:
        "analysis/global_phylogeny/bestrophins/ur50_aln_filtered.faa"
    output:
        "analysis/global_phylogeny/bestrophins/ur50_trim.faa"
    params:
        gt = 0.9
    conda:
        "envs/tools.yaml"
    shell:
        "trimal -in {input} -out {output} -gt {params.gt}"

rule best_iqtree:
    input:
        "analysis/global_phylogeny/bestrophins/ur50_trim.faa"
    output:
        "analysis/global_phylogeny/bestrophins/iqtree/{seed}/ur50.treefile",
        "analysis/global_phylogeny/bestrophins/iqtree/{seed}/ur50.log"
    params:
        prefix = "analysis/global_phylogeny/bestrophins/iqtree/{seed}/ur50",
        bb = 1000,
        pers = 0.2,
        nstop = 500,
    threads:
        2
    conda:
        "envs/tools.yaml"
    shell:
        "iqtree2 -s {input} -pre {params.prefix} -seed {wildcards.seed} -bb {params.bb} -T {threads} -redo -pers {params.pers} -nstop {params.nstop}"

rule best_best_iqtree:
    input:
        log_files  = expand("analysis/global_phylogeny/bestrophins/iqtree/{seed}/ur50.log", seed = best_seeds),
        tree_files = expand("analysis/global_phylogeny/bestrophins/iqtree/{seed}/ur50.treefile", seed = best_seeds)
    output:
        log_file  = "output/global_phylogeny/bestrophins_ur50_iqtree.log",
        tree_file = "output/global_phylogeny/bestrophins_ur50_iqtree.treefile"
    conda:
        "envs/r.yaml"
    script:
        "scripts/select_global_tree.R"

rule best_get_taxonomy:
    input:
        "input/global_phylogeny/bestrophins/bestrophins/uniref50.fasta"
    output:
        "analysis/global_phylogeny/bestrophins/uniref50.taxonomy"
    conda:
        "envs/r.yaml"
    script:
        "scripts/get_taxonomy.R"

rule best_plot_tree:
    input:
        tree     = "output/global_phylogeny/bestrophins_ur50_iqtree.treefile",
        metadata = "input/global_phylogeny/bestrophins/bestrophins.txt",
        taxa     = "input/global_phylogeny/taxa.txt",
        genes    = "input/global_phylogeny/bestrophins/bestrophins.txt",
        taxonomy = "analysis/global_phylogeny/bestrophins/uniref50.taxonomy",
        tab      = "input/global_phylogeny/bestrophins/bestrophins/uniref50.txt",
        fasta    = "input/global_phylogeny/bestrophins/bestrophins/uniref50.fasta",
        clstr    = "analysis/global_phylogeny/bestrophins/ur50_long.cdhit.clstr",
        targetp  = "analysis/global_phylogeny/bestrophins/predictions/uniref50.targetp2",
        asafind  = "analysis/global_phylogeny/bestrophins/predictions/uniref50.asafind",
        blast    = "analysis/global_phylogeny/bestrophins/predictions/uniref50.blastp"
    output:
        "output/global_phylogeny/bestrophins_ur50_iqtree.pdf"
    conda:
        "envs/r.yaml"
    script:
        "scripts/plot_global_bestrophins.R"

rule best_signalp4:
    input:
        "input/global_phylogeny/bestrophins/bestrophins/uniref50.fasta"
    output:
        "analysis/global_phylogeny/bestrophins/predictions/uniref50.signalp"
    shell:
        "signalp -f short {input} > {output}"

rule best_targetp:
    input:
        "input/global_phylogeny/bestrophins/bestrophins/uniref50.fasta"
    output:
        "analysis/global_phylogeny/bestrophins/predictions/uniref50.targetp2"
    shell:
        "targetp -org pl -fasta {input} -stdout > {output}"

rule best_asafind:
    input:
        fasta = "input/global_phylogeny/bestrophins/bestrophins/uniref50.fasta",
        signalp = "analysis/global_phylogeny/bestrophins/predictions/uniref50.signalp"
    output:
        "analysis/global_phylogeny/bestrophins/predictions/uniref50.asafind"
    shell:
        "ASAFind.py -f {input.fasta} -p {input.signalp} -o {output}"

rule best_translate:
    input:
        "codons/fasta/bestrophins_core_CODON.fasta"
    output:
        "analysis/global_phylogeny/bestrophins/bestrhodopsins_bestrophins.faa"
    conda:
        "envs/tools.yaml"
    shell:
        "seqkit translate {input} | seqkit seq -go {output}"

rule best_makeblastdb:
    input:
        "analysis/global_phylogeny/bestrophins/bestrhodopsins_bestrophins.faa"
    output:
        "analysis/global_phylogeny/bestrophins/bestrhodopsins_bestrophins.faa.pdb"
    conda:
        "envs/tools.yaml"
    shell:
        "makeblastdb -in {input} -dbtype prot"

rule best_blast:
    input:
        "analysis/global_phylogeny/bestrophins/bestrhodopsins_bestrophins.faa.pdb",
        query = "input/global_phylogeny/bestrophins/bestrophins/uniref50.fasta",
        db = "analysis/global_phylogeny/bestrophins/bestrhodopsins_bestrophins.faa"
    output:
        "analysis/global_phylogeny/bestrophins/predictions/uniref50.blastp"
    params:
        evalue = 1e-100
    conda:
        "envs/tools.yaml"
    shell:
        "blastp -query {input.query} -db {input.db} -evalue {params.evalue} -outfmt 6 -out {output}"
